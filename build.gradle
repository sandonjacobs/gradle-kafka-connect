plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.gradle.plugin-publish" version "0.12.0"
    id "com.github.breadmoirai.github-release" version "2.2.12"
    id "com.github.ben-manes.versions" version "0.29.0"
}

group 'com.github.sandonjacobs'
version '0.8.2'

githubRelease {
    token = getProperty('github.token')
    owner = 'sandonjacobs'
    repo = 'gradle-kafka-connect'
    tagName = "v${version}"
    releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
    targetCommitish = 'main'
}

repositories {
    mavenCentral()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.35'
    implementation 'com.konghq:unirest-java:3.13.4'

    // Gradle Properties
    // It also has the Common library in it, thus the dependency here
    implementation "gradle.plugin.com.redpillanalytics:gradle-properties:1.0.2"

    // testing
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

gradlePlugin {
    plugins {
        kafkaConnect {
            id = 'com.github.sandonjacobs.gradle-kafka-connect'
            implementationClass = 'com.github.sandonjacobs.gradle.ConnectorPlugin'
        }
    }
}

pluginBundle {
    website = 'https://github.com/sandonjacobs/gradle-kafka-connect'
    vcsUrl = 'https://github.com/sandonjacobs/gradle-kafka-connect'
    description = 'Gradle Plugin to Manage and Deploy Kafka Connector Configurations'
    tags = ['kafka', 'confluent', 'connect']

    plugins {
        kafkaConnect {
            displayName = 'gradle-kafka-connect'
            description = 'Gradle Plugin to Manage and Deploy Kafka Connector Configurations'
            tags = ['kafka', 'confluent', 'connect']
            version = project.version
        }
    }
}
